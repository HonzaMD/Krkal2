; Script generated by the HM NIS Edit Script Wizard.

!define KRKAL_VERSION "2"
!define KERNEL_VERSION "4"
!define PATCH_FROM_VERSION "2.2 nebo 2.3"
!define OLDCRC 1685343251

!define PRODUCT_NAME "KRKAL"
!define PRODUCT_VERSION "${KRKAL_VERSION}.${KERNEL_VERSION}"
!define PRODUCT_PUBLISHER "KRKALteam"
!define PRODUCT_WEB_SITE "http://krkal.org"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\KRKAL.exe"

!define KRKALPATH "D:\My Progs\KRKAL"
!define UPDATERPATH "${KRKALPATH}\updater"
!define LICENCE "${UPDATERPATH}\licence.rtf"
!define UPDATE_FILLES "${UPDATERPATH}\files${KRKAL_VERSION}${KERNEL_VERSION}"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${KRKALPATH}\res\krkal.ico"

; Welcome page
!define MUI_WELCOMEPAGE_TEXT "Tento prùvodce Vás bude provázet aktualizací programu KRKAL na verzi ${PRODUCT_VERSION}. Update je urèen pro verzi ${PATCH_FROM_VERSION}.\r\n\r\nPøed instalací ukonèete KRKALa!\r\n\r\nPøi aktualizaci budou pøepsány nìkteré soubory (levely, skripty) ze staré verze, pokud jste je mìnili, tak si je nejprve zálohujte. Novì vytvoøené levely zùstanou beze zmìny.\r\n\r\nPro pokraèování kliknìte na 'Další'."
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "${LICENCE}"
; Directory page
;!define MUI_DIRECTORYPAGE_TEXT_TOP "test"
;!define MUI_DIRECTORYPAGE_TEXT_DESTINATION "test"
!define MUI_DIRECTORYPAGE_VERIFYONLEAVE
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE VerifyDir
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_RUN_TEXT "Spustit KRKALa"
!define MUI_FINISHPAGE_RUN "$INSTDIR\KRKAL.exe"
!insertmacro MUI_PAGE_FINISH

; Language files
!insertmacro MUI_LANGUAGE "Czech"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION} (update)"
OutFile "KRKALupdate_${KRKAL_VERSION}.${KERNEL_VERSION}.exe"
InstallDir "$PROGRAMFILES\KRKAL\"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show

var DetectedVersion

Function VerifyDir
    IfFileExists $INSTDIR\KRKAL.exe PathGood
      MessageBox MB_OK|MB_ICONEXCLAMATION "Zadejte cestu k systému KRKAL!"
      Abort ; if $INSTDIR is not a KRKAL directory, don't let us install there
    PathGood:

      FileOpen $9 $INSTDIR\version r
      IfErrors noVersion
      FileRead $9 $0
      FileClose $9
      
      goto cmpVersion
      
      noVersion:
      StrCpy $0 "KRKAL v2.2"
      
      cmpVersion:
      
      ClearErrors
      StrCmp $0 "KRKAL v2.2" VersionOk22
      StrCmp $0 "KRKAL v2.3" VersionOk23

      MessageBox MB_OK|MB_ICONEXCLAMATION "Špatná verze, tento patch je urèen pro verzi ${PATCH_FROM_VERSION}!$\nNainstalován je ale $0."
      Abort

      VersionOk22:

      CRCCheck::GenCRC "$INSTDIR\KRKAL.exe"
      Pop $R1
      
      IntCmpU $R1 ${OLDCRC} CRCok
      
      MessageBox MB_YESNO|MB_ICONEXCLAMATION|MB_DEFBUTTON2 "Nesouhlasí kontrolní souèet souboru KRKAL.exe. Update na novou verzi se nemusí podaøit. Chtete se pokusit nainstalovat patch?" IDYES CRCok
      Abort
      
      CRCok:
      IntOp $DetectedVersion 2 + 0
      goto end
      
      VersionOk23:
      IntOp $DetectedVersion 3 + 0
      goto end
      
      end:
    
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite on
  
  IntCmp $DetectedVersion 2 ver22
  IntCmp $DetectedVersion 3 ver23
  goto end


  ver22:

  Rename "$INSTDIR\Doc" "$INSTDIR\Dokumentace"

  Delete "$INSTDIR\Games\Krkal_4F88_78B7_A01C_48AB\empty_E539_D0ED_27CF_4921.kc"
  Delete "$INSTDIR\Games\Krkal_4F88_78B7_A01C_48AB\MDtest_0001_FFFF_0001_0001.kc"
  Delete "$INSTDIR\Games\Krkal_4F88_78B7_A01C_48AB\moves_45FA_87CA_1260_EA1E.kc"
  RMdir /r "$INSTDIR\Games\Krkal_4F88_78B7_A01C_48AB\(56)Ledove Mesto_7D6C_B83F_55B5_FA6E.lv"
  
  ver23:

  File /r "${UPDATE_FILLES}\*.*"
  
  end:
  
SectionEnd

Section -Post
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\KRKAL.exe"
SectionEnd
